 10  DIM R(9)
 20  DIM C(9)
 30  DIM U(9)
 40 T = 0:W = 0:D = 0
 70  TEXT : HOME 
 80  VTAB (10): PRINT  TAB( 13)"TIC TAC TOE": PRINT : PRINT "Created by Mr. Bauman, Mar. 5, 1988": PRINT : PRINT : INPUT "Do you want to be X or O ? ";A$
 90  IF A$ <  > "X" AND A$ <  > "O" THEN  PRINT "Please try that again.": GOTO 80
 100  HOME : REM *** DRAW LINES
 110  HGR : HCOLOR= 3
 120  HPLOT 80,0 TO 80,120
 130  HPLOT 120,0 TO 120,120
 140  HPLOT 40,40 TO 160,40
 150  HPLOT 40,80 TO 160,80
 151  HPLOT 42,1 TO 42,6
 152  HPLOT 82,1 TO 85,1 TO 85,3 TO 82,6 TO 85,6
 153  HPLOT 122,1 TO 125,1 TO 125,3 TO 123,3 TO 125,3 TO 125,6 TO 122,6
 154  HPLOT 42,41 TO 42,44 TO 45,44 TO 45,42 TO 45,46
 155  HPLOT 85,41 TO 82,41 TO 82,43 TO 84,43 TO 85,44 TO 85,45 TO 84,46 TO 82,46
 156  HPLOT 122,41 TO 122,46 TO 125,46 TO 125,43 TO 122,43
 157  HPLOT 42,81 TO 45,81 TO 45,82 TO 43,86
 158  HPLOT 82,81 TO 82,86 TO 85,86 TO 85,81 TO 82,81 TO 82,83 TO 85,83
 159  HPLOT 122,81 TO 125,81 TO 125,86 TO 125,84 TO 122,84 TO 122,81
 160  IF A$ = "X" THEN 2000
 170  IF A$ = "O" THEN  GOTO 3000
 200  REM ***DRAW X
 210  HPLOT (M - 15),(N - 15) TO (M + 15),(N + 15)
 220  HPLOT (M - 15),(N + 15) TO (M + 15),(N - 15): RETURN 
 230  REM *** DRAW O
 240  FOR A = 0 TO 15
 250 B =  SQR (225 - A * A)
 260 X = M + A:Y = N + B: HPLOT X,Y
 270 X = M - A:Y = N + B: HPLOT X,Y
 280 X = M + A:Y = N - B: HPLOT X,Y
 290 X = M - A:Y = N - B: HPLOT X,Y
 300  NEXT A
 310 X = 0: RETURN 
 400  REM ***TABLE OF POSITIONS
 410  IF Q = 1 THEN M = 60:N = 20:H = 4: RETURN 
 420  IF Q = 2 THEN M = 100:N = 20:H = 3: RETURN 
 430  IF Q = 3 THEN M = 140:N = 20:H = 8: RETURN 
 440  IF Q = 4 THEN M = 60:N = 60:H = 9: RETURN 
 450  IF Q = 5 THEN M = 100:N = 60:H = 5: RETURN 
 460  IF Q = 6 THEN M = 140:N = 60:H = 1: RETURN 
 470  IF Q = 7 THEN M = 60:N = 100:H = 2: RETURN 
 480  IF Q = 8 THEN M = 100:N = 100:H = 7: RETURN 
 490  IF Q = 9 THEN M = 140:N = 100:H = 6: RETURN 
 500  PRINT "I think you pressed the wrong key.": PRINT "Try again.": RETURN 
 600  REM   *** COORDINATES FOR H
 610  IF H = 4 THEN M = 60:N = 20: RETURN 
 620  IF H = 3 THEN M = 100:N = 20: RETURN 
 630  IF H = 8 THEN M = 140:N = 20: RETURN 
 640  IF H = 9 THEN M = 60:N = 60: RETURN 
 650  IF H = 5 THEN M = 100:N = 60: RETURN 
 660  IF H = 1 THEN M = 140:N = 60: RETURN 
 670  IF H = 2 THEN M = 60:N = 100: RETURN 
 680  IF H = 7 THEN M = 100:N = 100: RETURN 
 690  IF H = 6 THEN M = 140:N = 100: RETURN 
 700 H = 1 +  INT (9 *  RND (1))
 710  IF R(H) = 1 THEN 700
 720  RETURN 
 800  IF U(1) <  > 5 THEN C(1) = 5:H = 5: GOTO 840
 810  IF U(1) <  > 5 THEN 800
 820  IF U(1) = 5 THEN  GOSUB 700: IF H = 2 OR H = 4 OR H = 6 OR H = 8 THEN C(1) = H: GOTO 840
 830  GOTO 820
 840 R(H) = 1: GOSUB 600: GOSUB 230: GOTO 2000
 850  IF U(1) = 5 THEN 940
 852 K = 15 - (U(1) + U(2)): IF K < 10 AND K > 0 THEN H = K: IF R(H) = 0 THEN C(2) = H: GOTO 840
 854 K = U(1) + U(2): IF K = 4 OR K = 8 OR K = 12 OR K = 16 THEN C(2) = 15 - (K / 2 + 5):H = C(2): GOTO 840
 856  IF U(1) = 1 OR U(1) = 3 OR U(1) = 7 OR U(1) = 9 THEN 859
 858 K = 15 - (U(1) + C(1)): IF K < 10 AND K > 0 THEN H = K: IF R(H) = 0 THEN C(2) = H: GOTO 840
 859 K = 15 - (U(2) + C(1)): IF K < 10 AND K > 0 THEN H = K: IF R(H) = 0 THEN C(2) = H: GOTO 840
 860  IF U(1) + U(2) <  > 10 THEN 910
 870  IF U(1) = 2 OR U(1) = 4 OR U(1) = 6 OR U(1) = 8 THEN  GOSUB 700: IF H = 1 OR H = 3 OR H = 5 OR H = 7 OR H = 9 THEN C(2) = H: GOTO 840
 880  IF U(1) = 2 OR U(1) = 4 OR U(1) = 6 OR U(1) = 8 THEN 870
 890  IF U(1) = 1 OR U(1) = 3 OR U(1) = 7 OR U(1) = 9 THEN  GOSUB 700: IF H = 2 OR H = 4 OR H = 6 OR H = 8 THEN C(2) = H: GOTO 840
 900  GOTO 890
 910  GOSUB 700: IF H = 2 OR H = 4 OR H = 6 OR H = 8 THEN C(2) = H: GOTO 840
 920  GOTO 910
 940 K = 15 - (U(1) + U(2)): IF K < 10 AND K > 0 THEN H = K: IF R(H) = 0 THEN C(2) = H: GOTO 840
 950  GOSUB 700: IF H = 2 OR H = 4 OR H = 6 OR H = 8 THEN C(2) = H: GOTO 840
 960  GOTO 950
 970 K = 15 - (C(1) + C(2)): IF K < 10 AND K > 0 THEN H = K: IF R(H) = 0 THEN C(3) = H:W = W + 1: GOSUB 600: GOSUB 230: GOTO 2200
 980 K = 15 - (U(2) + U(3)): IF K < 10 AND K > 0 THEN H = K: IF R(H) = 0 THEN C(3) = H: GOTO 840
 990 K = 15 - (U(1) + U(3)): IF K < 10 AND K > 0 THEN H = K: IF R(H) = 0 THEN C(3) = H: GOTO 840
 1000  GOSUB 700:C(3) = H: GOTO 840
 1010 K = 15 - (C(1) + C(3)): IF K < 10 AND K > 0 THEN H = K: IF R(H) = 0 THEN C(4) = H:W = W + 1: GOSUB 600: GOSUB 230: GOTO 2200
 1020 K = 15 - (C(2) + C(3)): IF K < 10 AND K > 0 THEN H = K: IF R(H) = 0 THEN C(4) = H:W = W + 1: GOSUB 600: GOSUB 230: GOTO 2200
 1030 K = 15 - (U(1) + U(4)): IF K < 10 AND K > 0 THEN H = K: IF R(H) = 0 THEN C(4) = H: GOTO 2100
 1040 K = 15 - (U(2) + U(4)): IF K < 10 AND K > 0 THEN H = K: IF R(H) = 0 THEN C(4) = H: GOTO 2100
 1050 K = 15 - (U(3) + U(4)): IF K < 10 AND K > 0 THEN H = K: IF R(H) = 0 THEN C(4) = H: GOTO 2100
 1060  GOSUB 700:C(4) = H: GOTO 2100
 1200 R(H) = 1: GOSUB 600: GOSUB 200: RETURN 
 2000  REM   *** GAME WHEN USER IS X
 2010  VTAB (24): PRINT "Enter the number for your X": INPUT Q
 2020  IF Q > 9 OR Q < 1 THEN  PRINT "I think you typed the wrong thing.  Try": INPUT "again.  The number for your X is ";Q
 2030  IF Q > 9 OR Q < 1 THEN 2020
 2035  GOSUB 400: IF R(H) = 1 THEN 2010
 2040  GOSUB 400: GOSUB 200
 2050 T = T + 1:U(T) = H:R(H) = 1
 2060  IF T = 1 THEN  GOTO 800
 2070  IF T = 2 THEN  GOTO 850
 2080  IF T = 3 THEN  GOTO 970
 2090  IF T = 4 THEN  GOTO 1010
 2100 R(H) = 1:D = D + 1: GOSUB 600: GOSUB 230: GOSUB 700: GOSUB 600: GOSUB 200: GOTO 2200
 2200  PRINT : PRINT  TAB( 15)"SCOREBOARD": PRINT "Computer: ";W;"      You: 0      Tie: ";D
 2210  PRINT : INPUT "Want another game? (Y/N) ";Q$
 2220  IF Q$ = "Y" OR Q$ = "y" THEN 2250
 2230  IF Q$ = "N" OR Q$ = "n" THEN  TEXT : HOME : END 
 2240  GOTO 2210
 2250  FOR H = 1 TO 9:R(H) = 0:U(H) = 0:C(H) = 0: NEXT H:T = 0: GOTO 70
 2300  GOSUB 700: IF H = 2 OR H = 4 OR H = 5 OR H = 6 OR H = 8 THEN C(1) = H: GOSUB 1200: GOTO 2900
 2310  GOTO 2300
 2320  IF R(5) = 0 THEN C(2) = 5:H = 5: GOSUB 1200: GOTO 2900
 2330  IF C(1) = 5 AND U(1) <  > 2 AND U(1) <  > 4 AND U(1) <  > 6 AND U(1) <  > 8 THEN  GOTO 2345
 2340  GOSUB 700:C(2) = H: GOSUB 1200: GOTO 2900
 2345  GOSUB 700: IF H = 2 OR H = 4 OR H = 6 OR H = 8 THEN C(2) = H: GOSUB 1200: GOTO 2900
 2346  GOTO 2345
 2350 K = 15 - (C(1) + C(2)): IF K > 0 AND K < 10 THEN H = K: IF R(H) = 0 THEN C(3) = H:W = W + 1: GOSUB 1200: GOTO 2200
 2360 K = 15 - (U(1) + U(2)): IF K > 0 AND K < 10 THEN H = K: IF R(H) = 0 THEN C(3) = H: GOSUB 1200: GOTO 2900
 2365  IF C(1) = 5 OR C(2) = 5 THEN 2390
 2370  GOSUB 700: IF H = 2 OR H = 4 OR H = 6 OR H = 8 THEN C(3) = H: GOSUB 1200: GOTO 2900
 2380  GOTO 2370
 2390  GOSUB 700: IF (C(1) + U(1) + H <  > 15) AND (C(1) + U(2) + H <  > 15) AND (C(2) + U(1) + H <  > 15) AND (C(2) + U(2) + H <  > 15) THEN C(3) = H: GOSUB 1200: GOTO 2900
 2395  GOTO 2390
 2400 K = 15 - (C(1) + C(3)): IF K > 0 AND K < 10 THEN H = K: IF R(H) = 0 THEN C(4) = H:W = W + 1: GOSUB 1200: GOTO 2200
 2410 K = 15 - (C(2) + C(3)): IF K > 0 AND K < 10 THEN H = K: IF R(H) = 0 THEN C(4) = H:W = W + 1: GOSUB 1200: GOTO 2200
 2420 K = 15 - (U(1) + U(3)): IF K > 0 AND K < 10 THEN H = K: IF R(H) = 0 THEN C(4) = H: GOSUB 1200: GOTO 2900
 2430 K = 15 - (U(2) + U(3)): IF K > 0 AND K < 10 THEN H = K: IF R(H) = 0 THEN C(4) = H: GOSUB 1200: GOTO 2900
 2440  GOSUB 700:C(4) = H: GOSUB 1200: GOTO 2900
 2900  VTAB (24): INPUT "Enter the number for your O ";Q
 2910  IF Q > 9 OR Q < 1 THEN  PRINT "I think you typed the wrong thing.  Try": INPUT "again.  The number for your O is ";Q
 2920  IF Q > 9 OR Q < 1 THEN 2910
 2930  GOSUB 400: IF R(H) = 1 THEN 2900
 2940  GOSUB 400: GOSUB 230:U(T) = H:R(H) = 1
 3000  REM *** GAME WHEN USER IS O
 3010 T = T + 1
 3020  IF T = 1 THEN  GOTO 2300
 3030  IF T = 2 THEN  GOTO 2320
 3040  IF T = 3 THEN  GOTO 2350
 3050  IF T = 4 THEN  GOTO 2400
 3060  IF T = 5 THEN  GOSUB 700:C(5) = H: GOSUB 1200:D = D + 1: GOTO 2200
